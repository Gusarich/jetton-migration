#include "imports/stdlib.fc";
#include "opcodes.fc";
#include "params.fc";
#include "jetton-utils.fc";

const int ton_for_fee = 50000000;
global slice old_jetton_wallet;
global slice migration_master;
global slice recipient;

() transfer(int amount, int query_id) impure {
    send_raw_message(begin_cell()
        .store_uint(0x18, 6)
        .store_slice(old_jetton_wallet)
        .store_coins(ton_for_fee * 5)
        .store_uint(1, 1 + 4 + 4 + 64 + 32 + 1 + 1)
        .store_ref(begin_cell()
            .store_uint(op::transfer, 32)
            .store_uint(query_id, 64)
            .store_coins(amount)
            .store_slice(migration_master)
            .store_slice(recipient)
            .store_uint(0, 1)
            .store_coins(ton_for_fee * 4)
            .store_uint(0, 1)
            .store_slice(recipient)
        .end_cell())
    .end_cell(), 1);
}

() load_data() impure inline {
    slice ds = get_data().begin_parse();
    slice old_jetton_minter = ds~load_msg_addr();
    migration_master = ds~load_msg_addr();
    recipient = ds~load_msg_addr();
    cell wallet_code = ds~load_ref();
    old_jetton_wallet = calculate_user_jetton_wallet_address(my_address(), old_jetton_minter, wallet_code);
}

() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if ((in_msg_body.slice_bits() == 0)) {
        return (); ;; ignore empty messages / not enough coins
    }
    throw_if(505, msg_value < ton_for_fee * 6);

    load_data();
    slice cs = in_msg_full.begin_parse();

    int flags = cs~load_uint(4);
    slice sender = cs~load_msg_addr();
    if (flags & 1) {
        return (); ;; ignore bounced messages
    }

    int op = in_msg_body~load_uint(32);

    if (op == op::migrate) {
        if (~ equal_slices(sender, recipient)) {
            return (); ;; ignore messages not from recipient
        }
    }
    elseif (op == op::transfer_notification) {
        if (~ equal_slices(sender, old_jetton_wallet)) {
            return (); ;; ignore messages not from old_jetton_wallet
        }
    }
    else {
        return (); ;; ignore other opcodes
    }

    int query_id = in_msg_body~load_uint(64);
    int amount = in_msg_body~load_coins();

    transfer(amount, query_id);
    send_raw_message(begin_cell()
        .store_uint(0x18, 6)
        .store_slice(recipient)
        .store_coins(my_balance - ton_for_fee * 6)
        .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
    .end_cell(), 0);
}
